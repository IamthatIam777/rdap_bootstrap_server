plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java-library'
    id 'war'
    id 'maven'
}

import org.gradle.internal.jvm.Jvm

assert Jvm.current().javaVersion >= JavaVersion.VERSION_11: 'This project requires Java 11+'

group = 'ARIN'
description = 'RDAP Bootstrap Server'

version = '1000.0-SNAPSHOT'
if (project.hasProperty('artifactVersion')) {
    version = "${project.properties.artifactVersion}"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    maven { url 'https://repo.maven.apache.org/maven2' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation group: 'com.googlecode.java-ipv6', name: 'java-ipv6', version: '0.17'
    implementation group: 'net.ripe.ipresource', name: 'ipresource', version: '1.47'
    implementation group: 'commons-io', name: 'commons-io', version: '2.7'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    testImplementation group: 'junit', name: 'junit', version: '4.8.2'
}

uploadArchives {
    repositories.mavenDeployer {
        if (project.version.endsWith('SNAPSHOT')) {
            snapshotRepository(url: project.properties.mavenRepository) {
                authentication(userName: project.properties.mavenUser, password: project.properties.mavenPassword)
            }
        } else {
            repository(url: project.properties.mavenRepository) {
                authentication(userName: project.properties.mavenUser, password: project.properties.mavenPassword)
            }
        }
    }
}

bootJar {
    mainClassName = 'net.arin.rdap_bootstrap.spring.RdapBootstrapApp'
}

bootRun {
    systemProperties System.properties
}
